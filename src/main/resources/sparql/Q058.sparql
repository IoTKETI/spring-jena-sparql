#title:Q058 which are located on the genome next to a protein which is involved in spermatogenesis (right)
#ac:Q0P670,P56378
# ... |- PREV -|end ... begin|- SP -|end ... begin|- NEXT -| ...
SELECT ?entry WHERE {
{
    # gets the one on the left side of the spermatogenesis (previous to spermatogenesis)
	SELECT distinct ?entry WHERE {
	{
		SELECT ?sp_entry ?sp_gene_begin (MIN(?prev_distance) as ?min_distance_prev) ?chr WHERE {
		  ?sp_entry :classifiedWith term:KW-0744 . # spermatogenesis keyword
		  ?sp_entry :gene ?sp_gene .
		  ?sp_gene :begin ?sp_gene_begin .
		  ?sp_gene  :chromosome ?chr .
		  ?prev_entry :gene ?prev_gene .
		  ?prev_gene :chromosome ?chr . # the previous entry should be in the same chromosome
		  ?prev_gene :end ?prev_gene_end .
		  bind (?sp_gene_begin - ?prev_gene_end as ?prev_distance) . 
		  filter (?prev_distance > 0)  . # only takes the one on the right side (next) 
		}
		group by ?sp_entry ?sp_gene_begin ?chr
		}
		  ?entry :gene/:end ?entry_end . 
		  ?entry :gene/:chromosome ?chr .
		  bind (?sp_gene_begin - ?entry_end as ?entry_distance) .
		  filter(?entry_distance = ?min_distance_prev) .
		}
}
union #union both queries
{
    # gets the one on the right side of the spermatogenesis (next to spermatogenesis)
	SELECT distinct ?entry WHERE {
	{
	SELECT ?sp_entry ?sp_gene_end (MIN(?next_distance) as ?min_distance_next) ?chr WHERE {
	  ?sp_entry :classifiedWith term:KW-0744 . # spermatogenesis keyword
	  ?sp_entry :gene ?sp_gene .
	  ?sp_gene :end ?sp_gene_end .
	  ?sp_gene  :chromosome ?chr .
	  ?next_entry :gene ?next_gene .
	  ?next_gene :chromosome ?chr . # the next entry should be in the same chromosome
	  ?next_gene :begin ?next_gene_begin .
	  bind (?next_gene_begin - ?sp_gene_end as ?next_distance) . 
	  filter (?next_distance > 0)  . # only takes the one on the right side (next) 
	}
	group by ?sp_entry ?sp_gene_end ?chr
	}
	  ?entry :gene/:begin ?entry_begin . 
	  ?entry :gene/:chromosome ?chr .
	  bind (?entry_begin - ?sp_gene_end as ?entry_distance) .
	  filter(?entry_distance = ?min_distance_next) .
	}
}
}